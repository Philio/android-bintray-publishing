apply plugin: 'com.jfrog.bintray'

version = libraryVersion

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = bintrayUser
    key = bintrayApikey
    configurations = ['archives']
    dryRun = hasProperty('bintrayDryRun') ? bintrayDryRun : false
    publish = hasProperty('bintrayPublish') ? bintrayPublish : false
    override = hasProperty('bintrayOverride') ? bintrayOverride : false
    pkg {
        repo = bintrayRepo
        name = bintrayName
        version {
            name = libraryVersion
            desc = hasProperty('versionDesc') ? versionDesc : null
            released = hasProperty('versionDate') ? versionDate : new Date()
            gpg {
                sign = hasProperty('bintrayGpgSign') ? bintrayGpgSign : false
                passphrase = hasProperty('bintrayGpgPassphrase') ? bintrayGpgPassphrase : null
            }
            mavenCentralSync {
                sync = hasProperty('mavenCentralSync') ? mavenCentralSync : false
                user = ossUser
                password = ossPassword
                close = hasProperty(ossClose) ? ossClose : 1
            }
        }
    }
}
